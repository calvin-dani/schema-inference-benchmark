plugins {
    id 'java-library'
    id 'scala'
}

group 'cz.cuni.mff.ksi.nosql.s13e'
version '1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

sourceSets {
    model { }
}

dependencies {
    modelImplementation 'org.eclipse.core:org.eclipse.core.runtime:3.7.0'
    modelImplementation 'org.eclipse.emf:org.eclipse.emf.ecore:2.23.0'
    modelImplementation 'org.eclipse.emf:org.eclipse.emf.ecore.xmi:2.16.0'

    implementation sourceSets.model.output
    implementation configurations.modelImplementation

    implementation project(':utils:annotation')

    implementation "com.typesafe.play:play-json_$scalaVersion:2.7.4"
    implementation "com.typesafe.scala-logging:scala-logging_$scalaVersion:3.9.3"

    implementation "org.apache.spark:spark-sql_$scalaVersion:2.4.5"
    implementation "org.apache.spark:spark-core_$scalaVersion:2.4.5"
    implementation "org.mongodb.scala:mongo-scala-driver_$scalaVersion:4.2.3"
    implementation "org.mongodb.spark:mongo-spark-connector_$scalaVersion:3.0.1"

    def versionStrictly = { versionNumber ->
        return {
            version {
                strictly versionNumber
            }
        }
    }
    implementation 'com.fasterxml.jackson.core:jackson-core', versionStrictly('2.9.8')
    implementation 'com.fasterxml.jackson.core:jackson-databind', versionStrictly('2.9.8')
    implementation 'com.fasterxml.jackson.module:jackson-module-scala_2.12', versionStrictly('2.9.8')

    testImplementation "org.scalatest:scalatest_$scalaVersion:3.2.9"
    testRuntimeOnly 'org.junit.platform:junit-platform-engine:1.7.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.7.2'
    testRuntimeOnly 'co.helmethair:scalatest-junit-runner:0.1.9'
}

test {
    useJUnitPlatform {
        includeEngines 'scalatest'
        testLogging {
            events("passed", "skipped", "failed")
        }
    }
}